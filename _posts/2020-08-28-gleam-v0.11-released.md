---
author: Louis Pilfold
title: Gleam v0.11 released!
tags:
  - Languages
  - Gleam
---

It's time for another Gleam release! This time as well as taking a look at
what's new in the compiler, we're going to take a look at what's new in the
wider Gleam ecosystem too.


## Record update syntax

Previously when we wanted to update some of the fields on the record we would
need to create a new instance and manually copy across the fields that have
not changed.

```rust
pub fn level_up(pokemon: Pokemon) {
  let new_level = pokemon.level + 1
  let new_moves = moves_for(pokemon.species, new_level)
  Pokemon(
    level: new_level,
    moves: new_moves,
    name: pokemon.name,
    species: pokemon.species,
    item: pokemon.item,
  )
}
```

This is quite verbose! Worse, it's error prone and annoying to type every
time.

To remedy this problem [Quinn Wilton](https://github.com/QuinnWilton) has
added the record update syntax to Gleam, so now we only need to specify the
fields that change.

```rust
pub fn level_up(pokemon: Pokemon) {
  let new_level = pokemon.level + 1
  let new_moves = moves_for(pokemon.species, new_level)
  Pokemon(..pokemon, level: new_level, moves: new_moves)
}
```

All data in Gleam is immutable, so this syntax does not alter the values of
the existing record, instead it creates a new record with the original values
and the updated values.


## Numbers

[Tom Whatmore](https://github.com/tomwhatmore) has added some new features to
Gleam's numbers.

There's now syntaxes for binary, octal, and hexadecimal int literals.

```rust
// 4 ways of writing the int 15
let hexadecimal = 0xF
let decimal = 15
let octal = 0o17
let binary = 0b00001111
```

These might be handy in many situations such as when implementing
[a virtual machine][chip-8] or [a game of tetris][tetris] in Gleam.

[chip-8]: https://github.com/QuinnWilton/gleam-chip8
[tetris]: https://github.com/lpil/tetromino

Underscores can now be added to numbers, making larger numbers easier to read.

```rust
let billion = 1_000_000_000
let trillion_and_change = 1_000_000_000_000.57
```


## Type holes

Type holes can be used to give partial annotations to values.

Here we're saying that `x` is a List, but we're not saying what type the list
contains, leaving the compiler to infer it.

```rust
let x: List(_) = run()
```

This may be useful when adding annotations for documentation or to use a more
restrictive type than would be inferred by the compiler, as you can leave out
any parts of the annotation that are not important to you there.


## Compiler improvements

The majority of the compiler work in this release has been improvements to
existing features.

The formatter style has been improved in several ways, and the performance of
the formatter has been improved. One popular change is to how it formats
assignments that don't fit on a single line.

```rust
// Before
assert Ok(
  tuple(user, session),
) = application_registry.get_session(request.user_id)

// Now
assert Ok(tuple(user, session)) =
  application_registry.get_session(request.user_id)
```

Much better!

Additionally several error messages have been improved to include more
information on how to fix the problem, and a whole bunch of bug have been
squashed. Please see the
[changelog](https://github.com/gleam-lang/gleam/blob/master/CHANGELOG.md) for
more detail.


## HTTP

Outside of the compiler the big new thing in Gleam is the first release of
[Gleam HTTP][gleam-http]. Inspired by existing libraries such as Ruby's Rack,
Elixir's Raxx, and Haskell's WAI, Gleam HTTP provides a single well-typed
interface for creating web applications and clients while keeping the
underlying HTTP client and server code modular.

[gleam-http]: https://github.com/gleam-lang/http

In Gleam a HTTP service is a regular function that takes a HTTP request and
returns a HTTP response, where request and response are types defined by the
HTTP library.

#### Services

Here's a simplistic service that echoes back any body sent to it.

```rust
import gleam/http.{Request}

pub fn echo_service(request: Request(_)) {
  http.response(200)
  |> http.set_resp_body(request.body)
}
```

As a normal Gleam function it is easy to test this service, no special test
helpers or test server are required.

```rust
import gleam/should
import my_app

pub fn echo_test() {
  let response =
    http.default_req()
    |> http.set_req_body("Hello, world!")
    |> my_app.service

  response.status
  |> should.equal(200)

  response.body
  |> should.equal("Hello, world!")
}
```

Gleam's HTTP library also provides middleware, which can be used to add
additional functionality to a HTTP service.

```rust
import gleam/bit_builder
import gleam/http/middleware

pub fn service() {
  my_app.echo_service
  // Add a header
  |> middleware.prepend_resp_header("made-with", "Gleam")
  // Convert the response body type
  |> middleware.map_resp_body(bit_builder.from_bit_string)
  // Support PATCH, PUT, and DELETE from browsers
  |> middleware.method_override
}
```

#### Running a service

Once we have defined a service we want to run the service with a web server so
that it can handle HTTP requests from the outside world. To do this we use a
HTTP server adapter, such as one for the [Elli Erlang web server][elli].

[elli]: https://github.com/elli-lib/elli

```rust
import my_app
import gleam/http/elli

pub fn start() {
  elli.start(my_app.service, on_port: 4000)
}
```

Or one for the [Cowboy Erlang web server][cowboy].

[cowboy]: https://github.com/ninenines/cowboy


```rust
import my_app
import gleam/http/cowboy

pub fn start() {
  cowboy.start(my_app.service, on_port: 4000)
}
```

Or even using the adapter for the [Elixir's Plug interface][plug], so that a
Gleam HTTP service can be mounted within an Elixir Phoenix web application.

[plug]: https://github.com/elixir-plug/plug

```elixir
defmodule MyAppWeb.UserController do
  use MyAppWeb, :controller

  def show(conn, params) do
    conn
    |> GleamPlug.call_service(params, &:my_app.service/1)
  end
end
```

A list of all available server adapters can be found in the [Gleam HTTP
project's README](https://github.com/gleam-lang/http/#server-adapters).

If you would like to see more examples of Gleam HTTP being used to create a
web application see the [echo server][echo-server-example] on GitHub. It
includes all the above as well as other concepts such as routing and logging.

[echo-server-example]: https://github.com/gleam-lang/example-echo-server


#### HTTP clients

HTTP services are only half the story! We also can use the HTTP library to
make out own requests.

```rust
import gleam/http
import gleam/httpc

pub fn get_my_ip() {
  try response =
    http.default_req()
    |> http.set_method(http.Get)
    |> http.set_host("api.ipify.org")
    |> httpc.send
  Ok(response.body)
}
```

Here we've build a HTTP request and sent it using the `httpc` client adapter,
returning the response body if the request is successful.

A list of all available client adapters can be found in the [Gleam HTTP
project's README](https://github.com/gleam-lang/http/#client-adapters).


## Try it out

If you want to try out the new version of Gleam head over to the [installation
page][installation]. I'd love to hear how you find it and get your feedback so
Gleam can continue to improve.

Want to view some existing Gleam projects? Head on over to the
[awesome-gleam][awesome-gleam] list. Looking for something to build in
Gleam? Check out [the suggestions tracker][suggestions].

[awesome-gleam]: https://github.com/gleam-lang/awesome-gleam
[suggestions]: https://github.com/gleam-lang/suggestions/issues
[installation]: https://gleam.run/book/getting-started/installing-gleam.html


## Supporting Gleam

If you would like to help make strongly typed programming on the Erlang
virtual machine a production-ready reality please consider **[sponsoring
Gleam][sponsor]** via the GitHub Sponsors program.

[sponsor]: https://github.com/sponsors/lpil

This release would not have been possible without the support of all the
people who have [sponsored](https://github.com/sponsors/lpil) and contributed
to it, so a huge thank you to them.

Lastly, a huge thank you to the contributors to and sponsors of Gleam since
last release!

 - [Adam Bowen](https://github.com/adamnbowen)
 - [Adam Mokan](https://github.com/amokan)
 - [Ahmad Sattar](https://github.com/thehabbos007)
 - [Al Dee](https://github.com/scripttease)
 - [Arian Daneshvar](https://github.com/bees)
 - [Arno Dirlam](https://github.com/arnodirlam)
 - [Ben Myles](https://github.com/benmyles)
 - [Bruno Dusausoy](https://github.com/bdusauso)
 - [Bruno Michel](https://github.com/nono)
 - [Bryan Paxton](https://github.com/starbelly)
 - [Chris Young](https://github.com/worldofchris)
 - [Christian Meunier](https://github.com/tlvenn)
 - [Christian Wesselhoeft](https://github.com/xtian)
 - [Connor Schembor](https://github.com/cschembor3)
 - [Dave Lucia](https://github.com/davydog187)
 - [David McKay](https://github.com/rawkode)
 - [delucks](https://github.com/delucks)
 - [Eric Meadows-JÃ¶nsson](https://github.com/ericmj)
 - [Erik Terpstra](https://github.com/eterps)
 - [Gary Rennie](https://github.com/Gazler)
 - [Graeme Coupar](https://github.com/obmarg)
 - [Guilherme Pasqualino](https://github.com/ggpasqualino)
 - [Hasan YILDIZ](https://github.com/hsnyildiz)
 - [Hendrik Richter](https://github.com/hendi)
 - [Herdy Handoko](https://github.com/hhandoko)
 - [Ingmar Gagen](https://github.com/igagen)
 - [Ivar Vong](https://github.com/ivarvong)
 - [James MacAulay](https://github.com/jamesmacaulay)
 - [Jechol Lee](https://github.com/jechol)
 - [John Palgut](https://github.com/Jwsonic)
 - [JosÃ© Valim](https://github.com/josevalim)
 - [Clever Bunny LTD](https://github.com/cleverbunny)
 - [Lars Lillo Ulvestad](https://github.com/kodeFant)
 - [Lars Wikman](https://github.com/lawik)
 - [Leandro Cesquini Pereira](https://github.com/leandrocp)
 - [Mario Vellandi](https://github.com/mvellandi)
 - [mario](https://github.com/mario-mazo)
 - [Markus](https://github.com/markusfeyh)
 - [Matthew Cheely](https://github.com/MattCheely)
 - [Michael Jones](https://github.com/michaeljones)
 - [Mike Roach](https://github.com/mroach)
 - [Milad](https://github.com/slashmili)
 - [ontofractal](https://github.com/ontofractal)
 - [Parker Selbert](https://github.com/sorentwo)
 - [Peter Saxton](https://github.com/CrowdHailer)
 - [Quinn Wilton](https://github.com/QuinnWilton)
 - [Raphael Megzari](https://github.com/happysalada)
 - [Robin Mattheussen](https://github.com/romatthe)
 - [Sam Aaron](https://github.com/samaaron)
 - [Santi Lertsumran](https://github.com/mrgleam)
 - [Sasan Hezarkhani](https://github.com/gootik)
 - [Sascha Wolf](https://github.com/sascha-wolf)
 - [SaÅ¡a JuriÄ‡](https://github.com/sasa1977)
 - [Sean Jensen-Grey](https://github.com/seanjensengrey)
 - [Sebastian Porto](https://github.com/sporto)
 - [Shane Sveller](https://github.com/shanesveller)
 - [Shritesh Bhattarai](https://github.com/shritesh)
 - [Simone Vittori](https://github.com/simonewebdesign)
 - [Tim Buchwaldt](https://github.com/timbuchwaldt)
 - [Tom Whatmore](https://github.com/tomwhatmore)
 - [Tristan Sloughter](https://github.com/tsloughter)
 - [Tyler Wilcock](https://github.com/twilco)
 - [Wojtek Mach](https://github.com/wojtekmach)

Thanks for reading! Have fun! ðŸ’œ

